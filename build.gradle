allprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = '11'
    targetCompatibility = '11'

    ext {
        springBootVersion = '2.3.2.RELEASE'
        springCloudVersion = 'Hoxton.SR6'
        lombokVersion = '1.18.12'
        junitVersion = '5.6.2'
        junitPlatformVersion = '1.6.2'
        assertjVersion = '3.16.1'
        apacheCommons3Version = '3.9'
        mapStructVersion = '1.3.1.Final'
        mockitoVersion = '3.3.3'
    }

    repositories {
        mavenCentral()
    }
}

buildscript {
    ext{
        springBootDependencyManagementPluginVersion = '1.0.9.RELEASE'
        ltgtPluginVersion = '0.21'
    }
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath group: 'io.spring.gradle', name: 'dependency-management-plugin', version: springBootDependencyManagementPluginVersion
        classpath group: 'net.ltgt.gradle', name: 'gradle-apt-plugin', version: ltgtPluginVersion
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'jacoco'

    group = 'br.gabrielsmartins.smartpayment'
    version = '0.0.1-SNAPSHOT'

    dependencies {
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junitVersion
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitVersion
        testImplementation group: 'org.junit.platform', name: 'junit-platform-launcher', version: junitPlatformVersion
        testImplementation group: 'org.junit.platform', name: 'junit-platform-engine', version: junitPlatformVersion
        testImplementation group: 'org.assertj', name: 'assertj-core', version: assertjVersion
        testImplementation group: 'org.mockito', name: 'mockito-core', version: mockitoVersion

        implementation group: 'org.projectlombok', name: 'lombok', version: lombokVersion
        testCompileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
        annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion
        testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion

        implementation group: 'org.apache.commons', name: 'commons-lang3', version: apacheCommons3Version

        implementation group: 'org.mapstruct', name: 'mapstruct', version: mapStructVersion
        annotationProcessor group: 'org.mapstruct', name: 'mapstruct-processor', version: mapStructVersion
    }

    dependencyManagement {
        imports {
            mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
        }
    }

    test {
        useJUnitPlatform()
        finalizedBy(jacocoTestReport)
    }

    jacoco {
        toolVersion = '0.8.5'
    }

    jacocoTestReport {
        reports {
            html.enabled = true
            xml.enabled = true
            csv.enabled = false
            html.destination file("${buildDir}/jacocoHtml")
        }
    }
}

configurations {
    compileClasspath
}
